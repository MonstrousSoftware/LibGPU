plugins {
    id 'java'
    id 'application'
}

group = 'com.monstrous'
version = '1.0'

application {
    mainClass = 'com.monstrous.Launcher'
}

run {
    workingDir = rootProject.file('assets').path
    setIgnoreExitValue(true)
}


// ensure the assets folder is included in the fat jar
sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]

// make fat jar, ie with all dependencies included
jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes(
                'Main-Class':  'com.monstrous.Launcher'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenLocal();
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {

    implementation project(':libGPU')
    implementation 'com.github.jnr:jnr-ffi:2.2.17'
    implementation "com.github.MonstrousSoftware:java-to-webgpu:${j2w_version}"
}

